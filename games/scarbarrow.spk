game scarbarrow-incident [

	title {The Scarbarrow Incident}
	by {Sam Phillips}

	version 0.1
	created {today}

	{
		# Welcome
	}
	{
		It is a lovely day in the quaint village of Scarbarrow.
		The sun is shining and the birds are singing
	}

	it is sunny, warm, not raining
	basement is dark

	start in shed

	object dogs (dogs) [
		they are not barking
		describe
			dogs as (dogs|hounds),
			barking as (barking|howling|whining)
		{Somewhere in the distance (dogs) are (barking)}
	]
	object gardener(gardener) [
		he is not angry
	]
	location shed [

		describe shed as (garden shed|wooden shack)
		{
			a (messy|ramshackle) (shed)
		}

		add object crowbar (crowbar)[
			{a (hefty|sturdy) (iron|metal) crowbar}
		]

		object broken-glass (glass)[
			{a pile of broken glass and the remains of a model boat}
		]

		add object ship-bottle (bottle)[

			it is not clean, not broken

			{[
				{an (old|ancient) glass bottle}
				if(it is clean)[
					{with an intricate model ship inside}
				] else [
					{covered with (dust|grime)}
				]
			]}

			on((drop|break|smash) it)[
				it is broken
				remove ship-bottle
				add broken-glass
				{It smashes into a thousand pieces to reveal
					a tangled mess of wood, thread and paper}
			]
			on((wipe|clean|rub|polish) it (with sleeve))[
				it is clean
				{You remove the dust to revel the contents of
					the bottleâ€”an intricate model ship with sails and rigging}
			]
			on ((open|uncork) it)[
				{You'd need (a corkscrew|an opener) for that}
				// (25% chance of) [ // (1/3 of, 0.5 of)
				// 	do (drop it)
				// ]
			]
			// on (examine it)[
			// 	describe it
			// 	do (wipe it)
			// ]
		]

		exits
			south to garden
	]

	location garden [
		{
			a (slightly|somewhat|rather) overgrown kitchen garden
		}

		add object flowers (flowers) [
			they are wilted
			{[
				if (they are wilted)[
					{slightly wilted roses}
				] else [
					{perky red roses}
				]
			]}
			on((smell|sniff) them)[
				if(they are not wilted)[
					{they smell of flowers}
				] else [
					{they smell dried-out}
				]
			]
			on((get|pick) them)[
				{ouch! They are too prickly to pick}
			]
			on(water (them|roses))[
				{they seem to immediately spring back to life}
				they are not wilted
			]
		]

		object shards (shards)[
			it is broken
			{a pile of colourful clay shards. They may once have been a garden gnome}
			on((talk|chat) to it)[
				{are you really going to talk to a pile of broken pottery?}
			]
		]

		add object garden-gnome (gnome) [
			{a brightly painted garden gnome carrying a fishing rod}

			on((talk|chat) to it)[
				{he stares back at you without moving}
			]
			on((break|smash|kick|kill) it)[
				it is broken
				gardener is angry
				remove garden-gnome
				add shards
				{ the poor gnome shatters into pieces.
					That was a bit violent.
					Are you feeling better now? }
			]
		]

		add object watering-can (watering-can) [
			{a rusty, green watering can}
		]

		exits
			south to kitchen,
			north to shed,
			west to pond,
			east to tree
	]

	location pond [
		{
			a peaceful pond covered in lily-pads
		}
		add object fishes (fishes) [
			{golden fishes}
		]
		exits
			east to garden,
			north to veg-patch,
			south to conservatory
	]

	location veg-patch [
		{ a well-tended veg patch busting with cabbages and runner beans}
		add object carrots (carrots) [
			{rows of tasty-looking carrots}
		]
		add object cabbages (cabbages) [
			{fat, green and purple cabbages}
		]
		add object beans (beans) [
			{runner beans dangling from trellises}
		]
		exits
			south to pond
	]

	location tree [
		{
			the shadow of a giant oak tree
		}
		exits
			west to garden,
			north to orchard
	]

	location orchard [
		{
			a little orchard with a dozen apple and pear trees
		}
		add object apples (apples) [
			{bright red apples hanging from the tree}
		]
		add object pears (pears) [
			{juicy-looking golden pears}
		]
		exits
			south to tree
	]

	location kitchen [
		{
			a country-style kitchen
		}
		add object kitchen-table (table) [
			{a large oak table}
		]
		add object range (range) [
			{a hefty iron range}
		]
		add object rug (rug) [
			// is not rolled-up
			{a small frayed rug}
		]
		exits
			north to garden,
			south to hall,
			down to basement
	]

	location hall [
		{a hallway with a tiled floor and a steep staircase}
		add object umbrella (umbrella) [
			it is closed
			{a black umbrella with a bone handle}
		]
		exits
			north to kitchen,
			west to living-room,
			east to dining-room
	//		up to landing
	]

	location living-room [
		{a cosy living room with a handsome fireplace and a large sofa}
		add object sideboard (sideboard) [
			{a wooden sideboard}
		]
		exits
			east to hall,
			north to conservatory
	]

	location dining-room [
		{a smart-looking dining room with a large polished table}

		add object teapot (teapot) [
			it is not broken
			{an old clay teapot}
		]
		add object crockery (crockery) [
			it is not broken
			{fine china crockery}
		]
		add object cutlery (cutlery) [
			{silver cutlery}
		]
		exits
			north to study,
			west to hall
	]

	location study [
		{a small study with a view down the garden to the orchard.
			The walls are lined with books and there's a small desk by the window}

		add object books (books)[
			{a variety of books}
		]
		exits
			south to dining-room
	]

	location conservatory [
		{a glass conservatory with wicker chairs and a lovely view of the pond}
		add object torch (torch)[
			{a hefty rubber torch}
		]
		exits
			south to living-room,
			east to kitchen,
			north to pond
	]

	location basement [

		it is dark, dusty, cold

		{
			A dark and spooky basement
		}
		add object junk (junk-pile)[
			{various boxes and crates}
		]
		exits
			up to kitchen
	]
]
